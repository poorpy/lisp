COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ NEWLINE?}
WHITESPACE = _{(" "| NEWLINE)}

number = @{ int }
    int = { ("+" | "-")? ~ digit+ }
    digit = { '0' .. '9' }

symbol = @{ keyword | identifier }
    keyword = { "let" | "+" | "-" | "*" | "/" | "%" | "^" | "=" | "<" | ">" | "!" }
    identifier = { letter ~ (letter | "?" | "_" | "-")* }
    letter = _{ 'a' .. 'z' | 'A' .. 'Z' }

string = @{ "\"" ~ (escape | !("\"" | "\\") ~ ANY)* ~ "\"" }
    escape = _{ "\\" ~ ("\"" | "\\" | "/" | "n" | "r" | "t") }

sexpr = { "(" ~ expr* ~ ")" }

qexpr = { "{" ~ expr* ~ "}" }

expr = { number | symbol | string | sexpr | qexpr }

lisp = _{ SOI ~ (COMMENT| expr)* ~ EOI }
